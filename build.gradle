plugins {
    id 'application'
    id 'checkstyle'
    id 'com.commercehub.gradle.plugin.avro' version '0.21.0'
    id 'idea'
    id 'java'
}

group 'net.dainco'
version '1.0-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

application {
    mainClassName = 'net.dainco.Application'
    applicationName = 'app'
}

avro {
    fieldVisibility = "PRIVATE"
}

checkstyle {
    toolVersion '8.29'
    configFile file("config/checkstyle/checkstyle.xml")
}

checkstyleMain {
    source = fileTree('src/main/java')
}

sourceSets {
    testit {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    testitImplementation.extendsFrom testImplementation
    testitRuntimeOnly.extendsFrom runtimeOnly
}

task integrationTest(type: Test) {
    description = "Runs integration tests."
    group = "verification"

    testClassesDirs = sourceSets.testit.output.classesDirs
    classpath = sourceSets.testit.runtimeClasspath
    shouldRunAfter test
}

check.dependsOn integrationTest

test {
    useJUnitPlatform()
}

integrationTest {
    mustRunAfter test
    useJUnitPlatform()
}

dependencies {
    // Pre processors.
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.12'

    // Framework.
    implementation group: 'io.vertx', name: 'vertx-codegen', version: '3.9.2', classifier: 'processor'
    implementation group: 'io.vertx', name: 'vertx-config', version: '3.9.2'
    implementation group: 'io.vertx', name: 'vertx-rx-java2', version: '3.9.2'
    implementation group: 'io.vertx', name: 'vertx-web', version: '3.9.2'
    implementation group: 'io.vertx', name: 'vertx-web-client', version: '3.9.2'
    implementation group: 'com.google.inject', name: 'guice', version: '4.2.3'

    // Client.
    implementation group: 'com.google.cloud', name :'google-cloud-storage', version: '1.113.0'
    implementation group: 'io.vertx', name: 'vertx-redis-client', version: '3.9.2'
    implementation group: 'org.elasticsearch.client', name: 'elasticsearch-rest-high-level-client', version: '7.5.1'

    // Serialization and parsing.
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.11.2'
    implementation group: 'org.apache.avro', name: 'avro', version: '1.9.2'
    implementation group: 'org.apache.tika', name: 'tika-parsers', version: '1.24.1'
    implementation group: 'org.jsoup', name: 'jsoup', version: '1.13.1'
    implementation group: 'org.xerial.snappy', name: 'snappy-java', version: '1.1.7.6'

    // Utils.
    implementation group: 'com.google.guava', name: 'guava', version: '28.2-jre'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.12'

    // Log.
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

    // Test.
    testImplementation group: 'io.vertx', name: 'vertx-junit5', version: '3.9.2'
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.12'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.16.1'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.5.10'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.5.10'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.6.2'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.2'

    // Integration test.
    testitImplementation group: 'org.testcontainers', name: 'junit-jupiter',  version:'1.14.3'
    testitImplementation group: 'org.testcontainers', name: 'testcontainers', version:'1.14.3'
}
